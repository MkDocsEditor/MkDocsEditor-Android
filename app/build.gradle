apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'
apply plugin: 'kotlin-kapt'
apply plugin: 'androidx.navigation.safeargs'
apply plugin: 'dagger.hilt.android.plugin'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "de.markusressel.mkdocseditor"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        setProperty("archivesBaseName", "MkDocsEditor_v${versionName}_(${versionCode})")

        multiDexEnabled true

        // Required for LastAdapter
        buildFeatures.dataBinding true
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            debuggable false
        }

        debug {
            applicationIdSuffix ".debug"
            debuggable true
        }

        staging {
            initWith release
            applicationIdSuffix ".debugStaging"
            signingConfig signingConfigs.debug
            minifyEnabled true
            debuggable false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        useIR = true
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    buildFeatures {
        viewBinding true
        dataBinding true
    }

    packagingOptions {
        pickFirst 'META-INF/library-core_release.kotlin_module'
        pickFirst 'META-INF/core_release.kotlin_module'
        pickFirst 'META-INF/library_release.kotlin_module'
        exclude 'LICENSE.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
    }

    lintOptions {
        abortOnError false
    }
}

kapt {
    correctErrorTypes true
}

dependencies {
    implementation project(':data')

    // Kotlin Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.3"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.2"
    // RxFlow
    implementation "io.github.reactivecircus.flowbinding:flowbinding-android:$flowBindingVersion"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.0-beta02'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.0'
    implementation 'androidx.annotation:annotation:1.2.0'
    implementation 'androidx.browser:browser:1.3.0'
    implementation 'com.google.android.material:material:1.3.0'

    implementation 'androidx.emoji:emoji:1.1.0'
    implementation 'androidx.emoji:emoji-bundled:1.1.0'

    compileOnly "org.glassfish:javax.annotation:$javaxAnnotationVersion"

    // Hilt (Dagger wrapper)
    implementation "com.google.dagger:hilt-android:$hiltVersion"
    kapt "com.google.dagger:hilt-android-compiler:$hiltVersion"

    // Hilt WorkManager integration
    implementation 'androidx.hilt:hilt-work:1.0.0'
    // When using Kotlin.
    // deprecated (?)
    //kapt 'androidx.hilt:hilt-compiler:1.0.0'

    // SingleLiveEvent similar implementation
    implementation "com.github.hadilq.liveevent:liveevent:1.2.0"

    // MikePenz stuff
    //the core iconcis library (without any widgets)
    implementation "com.mikepenz:iconics-core:${iconicsVersion}@aar"
    //this adds all ui view widgets (IconicsButton, IconicsImageView, ...)
    implementation "com.mikepenz:iconics-views:${iconicsVersion}@aar"
    implementation 'com.mikepenz:material-design-iconic-typeface:2.2.0.6-kotlin@aar'

    // Android Architecture Components
    implementation "android.arch.navigation:navigation-fragment-ktx:1.0.0"
    implementation "android.arch.navigation:navigation-ui-ktx:1.0.0"

    // Android KTX
    implementation "androidx.core:core-ktx:$aacAndroidKtxVersion"
    implementation "androidx.fragment:fragment-ktx:1.3.4"
    implementation "androidx.collection:collection-ktx:1.1.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.3.1"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.3.1"
    implementation "androidx.lifecycle:lifecycle-common-java8:2.3.1"

    // WorkManager
    def work_version = "2.5.0"
    implementation "androidx.work:work-runtime-ktx:$work_version"
    // optional - Test helpers
    androidTestImplementation "androidx.work:work-testing:$work_version"

    // RxBus (event bus)
    implementation 'com.eightbitlab:rxbus:1.0.2'

    // Permissions
    implementation 'com.karumi:dexter:6.2.2'

    // Material Drawer
    implementation "com.mikepenz:materialdrawer:${materialdrawerVersion}"
    implementation "com.mikepenz:materialdrawer-nav:${materialdrawerVersion}"
    implementation "com.mikepenz:materialdrawer-iconics:${materialdrawerVersion}"

    // Epoxy - Recyclerview for dummies
    implementation "com.airbnb.android:epoxy:$epoxyVersion"
    implementation "com.airbnb.android:epoxy-databinding:$epoxyVersion"
    implementation "com.airbnb.android:epoxy-paging:$epoxyVersion"
    kapt "com.airbnb.android:epoxy-processor:$epoxyVersion"

    // Multi-FABs
    implementation "com.leinardi.android:speed-dial:3.1.1"

    // Dialogs
    implementation 'com.afollestad.material-dialogs:core:3.3.0'
    implementation 'com.afollestad.material-dialogs:input:3.3.0'
    implementation 'com.afollestad.material-dialogs:lifecycle:3.3.0'

    // Code editor
    def codeEditorVersion = "v4.0.1"
//    def codeEditorVersion = "master-SNAPSHOT"
    implementation("com.github.markusressel:KodeEditor:${codeEditorVersion}") { changing = true }

    // Syntax Highlighting
    def codeHighlighterVersion = "v3.0.0"
//    def codeHighlighterVersion = "master-SNAPSHOT"
    implementation("com.github.markusressel.KodeHighlighter:markdown:${codeHighlighterVersion}") {
        changing = true
    }

    // Tutorial messages
    implementation 'com.github.markusressel:TutorialTooltip:v2.0.0'

    // AboutLibraries ("About" screen)
    implementation("com.mikepenz:aboutlibraries:$aboutlibrariesVersion@aar") {
        transitive = true
    }

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    implementation 'android.arch.navigation:navigation-fragment:1.0.0'

    // Hilt instrumentation tests
    androidTestImplementation "com.google.dagger:hilt-android-testing:$hiltVersion"
    kaptAndroidTest "com.google.dagger:hilt-android-compiler:$hiltVersion"
    // Hilt local unit tests
    testImplementation "com.google.dagger:hilt-android-testing:$hiltVersion"
    kaptTest "com.google.dagger:hilt-android-compiler:$hiltVersion"
}

configurations.all() {
    // Check for updates every build
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}